plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.openapi.generator" version "7.5.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation "org.openapitools:jackson-databind-nullable:0.2.6"
	implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.21'
	implementation 'io.swagger.core.v3:swagger-core:2.2.21'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	compileOnly 'javax.servlet:servlet-api:2.5'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'

}

sourceSets {
	main {
		java {
			srcDir("$buildDir/generated/src/main/java")
		}
	}
}

tasks.compileJava {
	dependsOn(tasks.openApiGenerate)
}

tasks.named('test') {
	useJUnitPlatform()
}
tasks.named("openApiGenerate"){
	generatorName.set("spring")
	remoteInputSpec.set("https://developer.shaype.com/openapi/62560a1548e9da0041f9e009")
	outputDir.set("$buildDir/generated")
	apiPackage.set("shaype.openapi.example.api")
	modelPackage.set("shaype.openapi.example.model")
	configOptions.set([
			interfaceOnly: "true",
//			skipDefaultInterface: "true",
			returnSuccessCode: "true",
			dateLibrary: "java8"
	]) // for list of config option please refer https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/spring.md
}